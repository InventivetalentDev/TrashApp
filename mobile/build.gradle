apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.inventivetalent.trashapp"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode Integer.parseInt("4" + rootProject.ext.appVersionId)
        versionName rootProject.ext.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField('String', "PlayApiKey", PlayApiKey)
            resValue('string', "admob_unit_1", "ca-app-pub-3940256099942544/6300978111")
            resValue('string', "admob_unit_2", "ca-app-pub-3940256099942544/6300978111")
        }
        release {
            buildConfigField('String', "PlayApiKey", PlayApiKey)
            resValue('string', "admob_unit_1", AdMobUnit1)
            resValue('string', "admob_unit_2", AdMobUnit2)
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation project(':trashapp_common')
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.preference:preference:1.1.0-alpha05'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    wearApp project(':wear')
    implementation 'com.google.android.gms:play-services-wearable:16.0.1'
    implementation "androidx.preference:preference:1.1.0-alpha05"
    implementation 'org.osmdroid:osmdroid-android:6.1.0'
    implementation 'com.android.billingclient:billing:2.0.0'
    implementation 'com.google.android.gms:play-services-ads:17.2.1'
    implementation 'com.github.MKergall:osmbonuspack:6.6.0'
    implementation "androidx.room:room-runtime:2.1.0-alpha07"
    annotationProcessor "androidx.room:room-compiler:2.1.0-alpha07" // For Kotlin use kapt instead of annotationProcessor
    implementation 'com.android.support:multidex:1.0.3'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}
